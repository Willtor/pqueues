/* A Shavit Lotan priority queue written in DEF.
 * Like the original description in the paper this queue has a skiplist
 * as the underlying data-structure for ordering elements.
 * The data-structure is lock-free and quiescently consistent.
 */

import "stdio.h";
import "assert.h";

typedef state_t = enum
    | ACTIVE
    | DELETED
    ;

typedef node_ptr = volatile * volatile node;

typedef node =
    { val      i64,            // Value.
      state volatile state_t,  // Logical deletion state.
      toplevel i32,            // Height.
      next     [20]node_ptr    // Follow-list of nodes.
    };


export opaque
typedef sl_pq_t =
    { head  node,
      tail  node
    };

export
def sl_pq_print (set *sl_pq_t) -> void
begin
    var node = set.head.next[0];
    while unmark(node) != &set.tail do
        if is_marked(node.next[0]) then
            node = unmark(node).next[0];
        else
            node = unmark(node);
            printf("node[%d]: %lld\n", node.toplevel, node.val);
            node = node.next[0];
        fi
    od
end

/** Return a new fixed-height skip list.
 */
export
def sl_pq_create () -> *sl_pq_t
begin
    var slpq = new sl_pq_t;
    slpq.head.val = 0x8000000000000000I64;
    slpq.tail.val = 0x7FFFFFFFFFFFFFFFI64;
    for var i = 0; i < 20; ++i do
        slpq.head.next[i] = &slpq.tail;
        slpq.tail.next[i] = nil;
    od
    return slpq;
end

/** Add a node, lock-free, to the Shavit Lotan priority queue.
 */
export
def sl_pq_add (seed *u64, set *sl_pq_t, x i64) -> bool
begin
    var preds [20]node_ptr;
    var succs [20]node_ptr;
    var toplevel = random_level(seed, 20);
    var node node_ptr = nil;
    while true do
        if true == find(set, x, preds, succs) then
            delete node;
            return false;
        fi
        var node = new node;
        node.val = x;
        node.toplevel = toplevel;
        //if node == nil then node = node_create(x, toplevel); fi
        if node == nil then
            node = new node;
            node.val = x;
            node.toplevel = toplevel;
            node.state = ACTIVE;
        fi
        for var i = 0; i <= toplevel; ++i do
            node.next[i] = unmark(succs[i]);
        od
        var pred = preds[0];
        var succ = succs[0];
        if !__builtin_cas(&pred.next[0], unmark(succ), node) then
            continue;
        fi
        for var i = 1; i <= toplevel; ++i do
            while true do
                pred = preds[i];
                succ = succs[i];
                if __builtin_cas(&pred.next[i], unmark(succ), node) then
                    break;
                fi
                find(set, x, preds, succs);
            od
        od
        return true;
    od
end

/** Remove a node, lock-free, from the skiplist.
 */
export
def sl_pq_remove (set *sl_pq_t, x i64) -> bool
begin
    var preds [20]node_ptr;
    var succs [20]node_ptr;
    var succ node_ptr;
    while true do
        if false == find(set, x, preds, succs) then
            return false;
        fi
        var node_to_remove = succs[0];
        var marked bool;
        for var level = node_to_remove.toplevel;
            level >= 1;
            --level
        do
            succ = node_to_remove.next[level];
            marked = is_marked(succ);
            while !marked do
                __builtin_cas(&node_to_remove.next[level], unmark(succ),
                                mark(succ));
                succ = node_to_remove.next[level];
                marked = is_marked(succ);
            od
        od
        succ = node_to_remove.next[0];
        marked = is_marked(succ);
        while true do
            var i_marked_it = __builtin_cas(&node_to_remove.next[0],
                                            unmark(succ), mark(succ));
            succ = succs[0].next[0];
            marked = is_marked(succ);
            if i_marked_it then
                retire succs[0];
                find(set, x, preds, succs);
                return true;
            elif marked then
                return false;
            fi
        od
    od
end

/** Remove a node, lock-free, from the skiplist.  Leak the memory.
 */
export
def sl_pq_leaky_remove (set *sl_pq_t, x i64) -> bool
begin
    var preds [20]node_ptr;
    var succs [20]node_ptr;
    var succ node_ptr;
    while true do
        if false == find(set, x, preds, succs) then
            return false;
        fi
        var node_to_remove = succs[0];
        var marked bool;
        for var level = node_to_remove.toplevel;
            level >= 1;
            --level
        do
            succ = node_to_remove.next[level];
            marked = is_marked(succ);
            while !marked do
                __builtin_cas(&node_to_remove.next[level], unmark(succ),
                                mark(succ));
                succ = node_to_remove.next[level];
                marked = is_marked(succ);
            od
        od
        succ = node_to_remove.next[0];
        marked = is_marked(succ);
        while true do
            var i_marked_it = __builtin_cas(&node_to_remove.next[0],
                                            unmark(succ), mark(succ));
            succ = succs[0].next[0];
            marked = is_marked(succ);
            if i_marked_it then
                find(set, x, preds, succs);
                return true;
            elif marked then
                return false;
            fi
        od
    od
end

/** Pop the front node from the list.  Return true iff there was a node to pop.
 *  Leak the memory.
 */
export
def sl_pq_leaky_pop_min (set *sl_pq_t) -> bool
begin
    // FIXME: If "curr" from outside is used the code doesn't work.
    for var curr = unmark(set.head.next[0]); curr != &set.tail; curr = unmark(curr.next[0]) do
        if curr.state == DELETED then
            continue;
        fi
        var res = __builtin_cas(&curr.state, ACTIVE, DELETED);
        if res then 
            // Physically delete the node now.
            return sl_pq_leaky_remove(set, curr.val);
        fi
    od
    return false;
end

/** Pop the front node from the list.  Return true iff there was a node to pop.
 *  Don't leak the memory.
 */
export
def sl_pq_pop_min (set *sl_pq_t) -> bool
begin
    // FIXME: If "curr" from outside is used the code doesn't work.
    for var curr = unmark(set.head.next[0]); curr != &set.tail; curr = unmark(curr.next[0]) do
        if curr.state == DELETED then
            continue;
        fi
        var res = __builtin_cas(&curr.state, ACTIVE, DELETED);
        if res then 
            // Physically delete the node now.
            var removed = sl_pq_leaky_remove(set, curr.val);
            if removed then
                retire unmark(curr);
            fi
            return true;
        fi
    od
    return false;
end

def fast_rand (seed *u64) -> u64
begin
    var val = seed[0];
    if val == 0 then val = 1; fi

    val ^= val << 6;
    val ^= val >> 21;
    val ^= val << 7;

    seed[0] = val;
    return val;
end

def random_level (seed *u64, max u32) -> u32
begin
    var level = 1;
    while fast_rand(seed) % 2 == 0 && level < max do
        ++level;
    od
    return level - 1;
end

def find (set *sl_pq_t,
          key i64,
          preds [20]node_ptr,
          succs [20]node_ptr) -> bool
begin
    var marked, snip bool;
    var pred, curr, succ node_ptr = nil, nil, nil;
retry:
    while true do
        pred = &set.head;
        for var level = 19; level >= 0; --level do
            curr = unmark(pred.next[level]);
            while true do
                { succ, marked } = ref_and_markbit(curr.next[level]);
                while marked do
                    snip = __builtin_cas(&pred.next[level], curr, succ);
                    if !snip then goto retry; fi
                    curr = unmark(pred.next[level]);
                    { succ, marked } = ref_and_markbit(curr.next[level]);
                od
                if curr.val < key then
                    pred = curr;
                    curr = succ;
                else
                    break;
                fi
            od
            preds[level] = pred;
            succs[level] = curr;
        od
        return curr.val == key;
    od
end

def ref_and_markbit (ptr node_ptr) -> { node_ptr, bool } =
    { unmark(ptr), is_marked(ptr) };

def mark (ptr node_ptr) -> node_ptr =
    cast(type node_ptr, 0x1I64 | cast(type i64, ptr));

def unmark (ptr node_ptr) -> node_ptr =
    cast(type node_ptr, 0xFFFFFFFFFFFFFFFEI64 & cast(type i64, ptr));

def is_marked (ptr node_ptr) -> bool =
    cast(type bool, 0x1I64 & cast(type i64, ptr));