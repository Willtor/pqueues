import "forkscan.defi";
import "malloc.h";
import "pthread.h";
import "stdio.h";
import "time.h";
import "stdlib.h";

// Set data structures:
import "fhsl_lf.defi";
import "fhsl_b.defi";
import "stacktrack/skip-list.h";

typedef benchmark_t = enum
    | FHSL_LF
    | FHSL_B
    | STACKTRACK
    ;

typedef memory_policy_t = enum
    | POLICY_LEAKY
    | POLICY_RETIRE
    | POLICY_HAZARD
    | POLICY_STACKTRACK
    ;

typedef state_t = enum
    | STATE_WAIT
    | STATE_RUN
    | STATE_END
    ;

typedef config_t =
    {
        benchmark      benchmark_t,
        policy         memory_policy_t,
        duration_s     i32,
        thread_count   i32,
        init_size      i64,
        upper_bound    i64,
        update_rate    i32,
        set            *void
    };

typedef stats_t =
    {
        read_attempts     i64,
        read_successes    i64,
        insert_attempts   i64,
        insert_successes  i64,
        remove_attempts   i64,
        remove_successes  i64
    };

typedef per_thread_data_t =
    {
        config         *config_t,
        id             i32,
        state          volatile *state_t,
        stats          stats_t
    };

/** Return the time in seconds.
 */
def hires_timer () -> f64
begin
    var ts timespec;
    // FIXME: Need a convenient way to access C MACROs.
    if 0 != clock_gettime(/*CLOCK_MONOTONIC=*/1, &ts) then
        fprintf(stderr, "fatal: clock failed.\n");
        exit(1);
    fi
    var sec = cast(type f64, ts.tv_sec);
    var nsec = cast(type f64, ts.tv_nsec);
    return sec + nsec / (1000.0 * 1000.0 * 1000.0);
end

def fast_rand (seed *u64) -> u64
begin
    var val = seed[0];
    if val == 0 then val = 1; fi

    val ^= val << 6;
    val ^= val >> 21;
    val ^= val << 7;

    seed[0] = val;
    return val;
end

def string_of_benchmark (b benchmark_t) -> *char
begin
    switch b with
    xcase FHSL_LF: return "fhsl_lf";
    xcase FHSL_B: return "fhsl_b";
    xcase STACKTRACK: return "stacktrack";
    xcase _: return "unknown benchmark";
    esac
    return "FIXME: need reachability detection after a switch.";
end

def string_of_policy (p memory_policy_t) -> *char
begin
    switch p with
    xcase POLICY_LEAKY: return "leaky";
    xcase POLICY_RETIRE: return "retire";
    xcase POLICY_HAZARD: return "hazard";
    xcase POLICY_STACKTRACK: return "stacktrack";
    xcase _: return "unknown policy";
    esac
    return "FIXME: need reachability detection after a switch.";
end

def help (bench *char) -> void
begin
    printf("Usage: %s [OPTIONS]\n", bench);
    printf("  -h, --help: This help message.\n");
    printf("  -t <n>: Set the number of threads. (default = 1)\n");
    printf("  -d <n>: Benchmark duration in seconds. (default = 1)\n");
    printf("  -b <benchmark>: Set the benchmark. (default = fhsl_lf)\n");
    printf("     * fhsl_lf: Fixed-height skip list; lock-free.\n");
    printf("     * fhsl_b: Fixed-height skip list; blocking.\n");
    printf("     * stacktrack: Use the StackTrack skiplist written in C.\n");
    printf("  -p <mem_policy>: Set the memory policy. (default = leaky)\n");
    printf("     * leaky: Leak removed nodes.\n");
    printf("     * retire: Use Forkscan to reclaim removed nodes.\n");
    printf("     * hazard: Hazard pointers (for stacktrack bench only).\n");
    printf("     * stacktrack: StackTrack (for stacktrack bench only).\n");
    printf("  -i <n>: Initial set size. (default = 256)\n");
    printf("  -r <n>: Range upper bound [0-n). (default = 512)\n");
    printf("  -u <n>: Percent of ops that are updates. (default = 10)\n");
    exit(127);
end

/** Parse an i32 from txt in the range [low, high].  The err text is the
 *  command line option and is used in case of failure.
 */
def read_i32 (low i32, high i32, txt *char, err *char) -> i32
begin
    var n = atoi(txt);
    if n < low || n > high then
        fprintf(stderr, "error: %s requires an argument between %d and %d\n",
                err, low, high);
        exit(1);
    fi
    return n;
end

/** Parse an i64 from txt in the range [low, high].  The err text is the
 *  command line option and is used in case of failure.
 */
def read_i64 (low i64, high i64, txt *char, err *char) -> i64
begin
    var n = atoll(txt);
    if n < low || n > high then
        fprintf(stderr, "error: %s requires an argument between %lld and %lld\n",
                err, low, high);
        exit(1);
    fi
    return n;
end

def read_args (argc i32, argv **char) -> config_t
begin
    var config config_t =
        { FHSL_LF, POLICY_LEAKY, 1, 1, 256, 512, 10, nil };

    for var i = 1; i < argc; ++i do
        switch argv[i] with
        xcase "-h":
        ocase "--help":
            help(argv[0]); // no return.
        xcase "-t":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -t requires an argument.\n");
                exit(1);
            fi
            config.thread_count = read_i32(1, 128, argv[i], "-t");
        xcase "-d":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -d requires an argument.\n");
                exit(1);
            fi
            config.duration_s = read_i32(1, 999, argv[i], "-d");
        xcase "-b":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -b requires an argument.\n");
                exit(1);
            fi
            switch argv[i] with
            xcase "fhsl_lf": config.benchmark = FHSL_LF;
            xcase "fhsl_b": config.benchmark = FHSL_B;
            xcase "stacktrack": config.benchmark = STACKTRACK;
            xcase _:
                printf("unknown benchmark: %s\n", argv[i]);
                exit(1);
            esac
        xcase "-p":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -b requires an argument.\n");
                exit(1);
            fi
            switch argv[i] with
            xcase "leaky": config.policy = POLICY_LEAKY;
            xcase "retire":
            ocase "forkscan":
                config.policy = POLICY_RETIRE;
            xcase "hazard": config.policy = POLICY_HAZARD;
            xcase "stacktrack": config.policy = POLICY_STACKTRACK;
            xcase _:
                printf("unknown memory policy: %s\n", argv[i]);
                exit(1);
            esac
        xcase "-i":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -i requires an argument.\n");
                exit(1);
            fi
            config.init_size =
                read_i64(1, 0x7FFFFFFFFFFFFFFFI64, argv[i], "-i");
        xcase "-r":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -r requires an argument.\n");
                exit(1);
            fi
            config.upper_bound =
                read_i64(1, 0x7FFFFFFFFFFFFFFFI64, argv[i], "-r");
        xcase "-u":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -u requires an argument.\n");
                exit(1);
            fi
            config.update_rate = read_i32(0, 100, argv[i], "-u");
        xcase _:
            printf("unknown option: %s\n", argv[i]);
            exit(1);
        esac
    od

    return config;
end

def verify_config (config *config_t) -> void
begin
    // Be sure the configuration is valid -- i.e., does the given data
    // structure support the requested memory policy, etc.?

    switch { config.benchmark, config.policy } with
    xcase { FHSL_LF, POLICY_LEAKY }:
    ocase { FHSL_LF, POLICY_RETIRE }:
    ocase { FHSL_B, POLICY_LEAKY }:
    ocase { STACKTRACK, POLICY_LEAKY }:
    ocase { STACKTRACK, POLICY_RETIRE }:
    ocase { STACKTRACK, POLICY_HAZARD }:
    ocase { STACKTRACK, POLICY_STACKTRACK }:
    xcase _:
        printf("Unsupported configuration:\n");
        printf("  benchmark: %s\n  policy: %s\n",
               string_of_benchmark(config.benchmark),
               string_of_policy(config.policy));
        printf("No implementation for this combination.\n");
        exit(1);
    esac
end

def print_config (config *config_t) -> void
begin
    printf("Benchmark configuration\n");
    printf("--------- -------------\n");
    printf("  benchmark    : %s\n", string_of_benchmark(config.benchmark));
    printf("  mem_policy   : %s\n", string_of_policy(config.policy));
    printf("  duration (s) : %d\n", config.duration_s);
    printf("  thread count : %d\n", config.thread_count);
    printf("  initial size : %lld\n", config.init_size);
    printf("  range        : [0-%lld)\n", config.upper_bound);
    printf("  updates      : %d%%\n", config.update_rate);

    puts(""); // blank line.
end

def success_rate (attempts i64, successes i64) -> f64
begin
    if attempts == 0 then return 0.0F64; fi
    return cast(type f64, successes) * 100.0F64 / cast(type f64, attempts);
end

def print_stats (stats *stats_t, runtime f64) -> void
begin
    var total_ops i64 = 0;
    printf("  read-attempts      : %lld\n", stats.read_attempts);
    printf("  read-successes     : %lld (%.1f%%)\n", stats.read_successes,
           success_rate(stats.read_attempts, stats.read_successes));
    printf("  reads-per-second   : %lld\n",
           cast(type i64, stats.read_successes / runtime));
    printf("  insert-attempts    : %lld\n", stats.insert_attempts);
    printf("  insert-successes   : %lld (%.1f%%)\n", stats.insert_successes,
           success_rate(stats.insert_attempts, stats.insert_successes));
    printf("  inserts-per-second : %lld\n",
           cast(type i64, stats.insert_successes / runtime));
    printf("  remove-attempts    : %lld\n", stats.remove_attempts);
    printf("  remove-successes   : %lld (%.1f%%)\n", stats.remove_successes,
           success_rate(stats.remove_attempts, stats.remove_successes));
    printf("  removes-per-second : %lld\n",
           cast(type i64, stats.remove_successes / runtime));

    total_ops = stats.read_successes
        + stats.insert_successes
        + stats.remove_successes;

    printf("  total-operations   : %lld\n", total_ops);
    printf("  ops-per-second     : %lld\n",
           cast(type i64, total_ops / runtime));
end

def thread (arg *void) -> *void
begin
    var seed = cast(type u64, time(nil));
    var ptd = cast(type volatile *per_thread_data_t, arg);
    var stats stats_t = { 0, 0, 0, 0, 0, 0 };
    var config *config_t = ptd.config;
    var bench = config.benchmark;
    var policy = config.policy;
    var set = config.set;

    // StackTrack, since it was originally part of a different benchmark, does
    // things differently.  We aim to please.
    var st_thread = new st_thread_t;
    var st_seed = cast(type i32, seed);
    ST_thread_init(st_thread, &st_seed, 50, 100);

    printf("[started thread %d]\n", ptd.id);
    while ptd.state[0] == STATE_WAIT do
        // busy-wait.
    od
    var read_action = 100 - config.update_rate;
    var add_action = read_action + (config.update_rate / 2);
    while ptd.state[0] == STATE_RUN do
        var action = fast_rand(&seed) % 100;
        var val i64 = fast_rand(&seed) % config.upper_bound;
        switch bench with

/***************************************************************************/
/*                    fixed-height skip list, lock free                    */
/***************************************************************************/

        xcase FHSL_LF:
            if action < read_action then
                stats.read_attempts++;
                if fhsl_lf_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if fhsl_lf_add(&seed, set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                if policy == POLICY_LEAKY then
                    if fhsl_lf_leaky_remove(set, val) then
                        stats.remove_successes++;
                    fi
                else
                    // policy = POLICY_RETIRE
                    if fhsl_lf_remove(set, val) then
                        stats.remove_successes++;
                    fi
                fi
            fi

/***************************************************************************/
/*                     fixed-height skip list, blocking                    */
/***************************************************************************/

        xcase FHSL_B:
            if action < read_action then
                stats.read_attempts++;
                if fhsl_b_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if fhsl_b_add(&seed, set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                if fhsl_b_remove(set, val) then
                    stats.remove_successes++;
                fi
            fi

/***************************************************************************/
/*                    StackTrack fixed-height, blocking                    */
/***************************************************************************/

        xcase STACKTRACK:
            if action < read_action then
                stats.read_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if 0 != skiplist_contains_pure(st_thread, set, val) then
                        stats.read_successes++;
                    fi
                xcase POLICY_RETIRE:
                    if 0 != skiplist_contains_forkscan(st_thread, set, val)
                    then
                        stats.read_successes++;
                    fi
                xcase POLICY_HAZARD:
                    if 0 != skiplist_contains_hp(st_thread, set, val) then
                        stats.read_successes++;
                    fi
                xcase POLICY_STACKTRACK:
                    if 0 != skiplist_contains_stacktrack(st_thread, set, val)
                    then
                        stats.read_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            elif action < add_action then
                stats.insert_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if nil != skiplist_insert_pure(st_thread, set, val) then
                        stats.insert_successes++;
                    fi
                xcase POLICY_RETIRE:
                    if nil != skiplist_insert_forkscan(st_thread, set, val)
                    then
                        stats.insert_successes++;
                    fi
                xcase POLICY_HAZARD:
                    if nil != skiplist_insert_hp(st_thread, set, val) then
                        stats.insert_successes++;
                    fi
                xcase POLICY_STACKTRACK:
                    if nil != skiplist_insert_stacktrack(st_thread, set, val)
                    then
                        stats.insert_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if 0 != skiplist_remove_pure(st_thread, set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_RETIRE:
                    if 0 != skiplist_remove_forkscan(st_thread, set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_HAZARD:
                    if 0 != skiplist_remove_hp(st_thread, set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_STACKTRACK:
                    if 0 != skiplist_remove_stacktrack(st_thread, set, val)
                    then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
        xcase _:
            printf("error: unknown benchmark configuration.\n");
            exit(1);
        esac
    od

    // Cleanup StackTrack.
    ST_thread_finish(st_thread);
    delete st_thread;

    // Store this thread's statistics in the per-thread-data.
    ptd.stats = stats;
    return nil;
end

def initialize_set (config *config_t, seed *u64) -> void
begin
    switch config.benchmark with
    xcase FHSL_LF:
        var set = fhsl_lf_create();
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if false == fhsl_lf_add(seed, set, val) then
                --i;
            fi
        od
        config.set = set;
    xcase FHSL_B:
        var set *fhsl_b = fhsl_b_create();
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if false == fhsl_b_add(seed, set, val) then
                --i;
            fi
        od
        config.set = cast(type *void, set);
    xcase STACKTRACK:
        var set = skiplist_init();
        var st_thread = new st_thread_t;
        var local_seed = cast(type i32, seed[0]);
        ST_thread_init(st_thread, &local_seed, 50, 100);
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            switch config.policy with
            xcase POLICY_LEAKY:
                if nil == skiplist_insert_pure(st_thread, set, val) then
                    --i;
                fi
            xcase POLICY_RETIRE:
                if nil == skiplist_insert_forkscan(st_thread, set, val) then
                    --i;
                fi
            xcase POLICY_HAZARD:
                if nil == skiplist_insert_hp(st_thread, set, val) then
                    --i;
                fi
            xcase POLICY_STACKTRACK:
                if nil == skiplist_insert_stacktrack(st_thread, set, val) then
                    --i;
                fi
            xcase _:
                printf("error: unsupported mem policy for stacktrack.");
                exit(1);
            esac
        od
        ST_thread_finish(st_thread);
        delete st_thread;
        config.set = cast(type *void, set);
    xcase _:
        printf("error: unable to initialize unknown set.\n");
        exit(1);
    esac
end

export
def main (argc i32, argv **char) -> i32
begin
    var config = read_args(argc, argv);
    var seed = cast(type u64, time(nil));
    var state = STATE_WAIT;

    //forkscan_set_allocator(malloc, free, malloc_usable_size);

    verify_config(&config);
    print_config(&config);

    printf("Initializing set.\n");
    initialize_set(&config, &seed);

    printf("Starting threads.\n");
    var tids *pthread_t = new [config.thread_count]pthread_t;
    var ptds *per_thread_data_t = new [config.thread_count]per_thread_data_t;
    for var i = 0; i < config.thread_count; ++i do
        ptds[i] =
            { &config,
              i,
              &state,
              { 0, 0, 0, 0, 0, 0 }
            };
        var ret = pthread_create(&tids[i], nil, thread, &ptds[i]);
        if ret != 0 then
            printf("error: failed to create thread id: %d\n", i);
            exit(1);
        fi
    od

    puts("beginning");

    var start_time = hires_timer();
    state = STATE_RUN;
    // Robust sleep against Forkscan signals.
    forkscan_sleep(config.duration_s);
    state = STATE_END;

    puts("ending");
    printf("Joining threads.\n");
    for var i = 0; i < config.thread_count; ++i do
        var ret = pthread_join(tids[i], nil);
        if ret != 0 then
            printf("error: failed to join thread id: %d\n", i);
            exit(1);
        fi
        printf("[joined thread %d]\n", i);
    od
    var runtime = hires_timer() - start_time;

    // Print out the statistics.
    puts("Summary:");
    printf("  runtime (s) : %.9f\n", runtime);

    var totals stats_t = { 0, 0, 0, 0, 0, 0 };
    for var i = 0; i < config.thread_count; ++i do
        printf("statistics for thread %d\n", i);
        print_stats(&ptds[i].stats, runtime);
        totals.read_attempts += ptds[i].stats.read_attempts;
        totals.read_successes += ptds[i].stats.read_successes;
        totals.insert_attempts += ptds[i].stats.insert_attempts;
        totals.insert_successes += ptds[i].stats.insert_successes;
        totals.remove_attempts += ptds[i].stats.remove_attempts;
        totals.remove_successes += ptds[i].stats.remove_successes;
    od

    printf("total statistics:\n");
    print_stats(&totals, runtime);

    delete tids;
    delete ptds;
    return 0;
end
