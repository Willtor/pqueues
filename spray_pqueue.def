/* Fixed height skiplist: A skiplist implementation with an array of "next"
 * nodes of fixed height.
 */

import "stdio.h";
import "math.h";

typedef node_state_t = enum
    | PADDING
    | ACTIVE
    | DELETED
    ;

typedef node_t =
    {
      priority i64,                 // Key.
      toplevel i32,                 // Height.
      state volatile node_state_t,
      next     [20]*node_t          // Follow-list of nodes.
    };

export opaque
typedef spray_pqueue_config_t =
    {
      thread_count i64,
      start_height i64,
      max_jump i64,
      descend_amount i64,
      padding_amount i64
    };

// Strange BUG I found trying to initialise this struct
export
def spray_pqueue_config_paper(threads u64) -> spray_pqueue_config_t
begin
  var log_arg = threads;
  if threads == 1 then
    log_arg = 2;
  fi

  var spray_config spray_pqueue_config_t = {
    cast(type i64, threads), cast(type i64, log2(threads) + 1),
    cast(type i64, log2(threads) + 1), 1I64, 
    threads * cast(type i64, log2(log_arg))
  };
  return spray_config;
end

export opaque
typedef spray_pqueue_t =
    {
      config spray_pqueue_config_t,
      padding_head  *node_t,
      head  node_t,
      tail  node_t
    };

/** Print out the contents of the skip list along with node heights.
 */
export
def spray_pqueue_print (set *spray_pqueue_t) -> void
begin
    printf("HEAD: %lld ", set.head.priority);
    if set.head.state == DELETED then
        printf("DELETED\n");
    elif set.head.state == PADDING then
        printf("PADDING\n");
    else
        printf("ACTIVE\n");
    fi
    var node = set.head.next[0];
    while unmark(node) != &set.tail do
        if is_marked(node.next[0]) then
            node = unmark(node).next[0];
        else
            node = unmark(node);
            printf("node[%d]: %lld ", node.toplevel, node.priority);
            if node.state == DELETED then
                printf("DELETED\n");
            elif node.state == PADDING then
                printf("PADDING\n");
            else
                printf("ACTIVE\n");
            fi
            node = node.next[0];
        fi
    od
    printf("TAIL: %lld ", set.tail.priority);
    if set.tail.state == DELETED then
        printf("DELETED\n");
    elif set.tail.state == PADDING then
        printf("PADDING\n");
    else
        printf("ACTIVE\n");
    fi
end

/** Return a new fixed-height skip list.
 */
export
def spray_pqueue_create (threads u64) -> *spray_pqueue_t
begin
    var config = spray_pqueue_config_paper(threads);
    var spray_pqueue = new spray_pqueue_t;
    spray_pqueue.config = config;
    spray_pqueue.head.priority = 0x8000000000000000I64;
    spray_pqueue.head.state = PADDING;
    spray_pqueue.tail.priority = 0x7FFFFFFFFFFFFFFFI64;
    spray_pqueue.tail.state = PADDING;
    for var i = 0; i < 20; ++i do
        spray_pqueue.head.next[i] = &spray_pqueue.tail;
        spray_pqueue.tail.next[i] = nil;
    od
    // Insert dummy nodes
    spray_pqueue.padding_head = &spray_pqueue.head;
    for var i = 1; i < config.padding_amount; ++i do
        var padding_node = new node_t;
        padding_node.state = PADDING;
        // Dummy nodes are fully connected.
        for var j = 0; j < 20; ++j do
            padding_node.next[j] = spray_pqueue.padding_head;
        od
        spray_pqueue.padding_head = padding_node;
    od
    return spray_pqueue;
end

/** Add a node, lock-free, to the priority queue.
 */
export
def spray_pqueue_add (seed *u64, set *spray_pqueue_t, priority i64) -> bool
begin
    var preds [20]*node_t;
    var succs [20]*node_t;
    var toplevel = random_level(seed, 20);
    while true do
        if find(set, priority, preds, succs) then
            return false;
        else
            var node = new node_t;
            node.priority = priority;
            node.toplevel = toplevel;
            node.state = ACTIVE;
            for var i = 0; i <= toplevel; ++i do
                node.next[i] = unmark(succs[i]);
            od
            var pred = preds[0];
            var succ = succs[0];
            if !__builtin_cas(&pred.next[0], unmark(succ), node) then
                continue;
            fi
            for var i = 1; i <= toplevel; ++i do
                while true do
                    pred = preds[i];
                    succ = succs[i];
                    if __builtin_cas(&pred.next[i], unmark(succ), node) then
                        break;
                    fi
                    find(set, priority, preds, succs);
                od
            od
            return true;
        fi
    od
    return true;
end

/** Remove a node, lock-free, from the skiplist.
 */
export
def spray_pqueue_remove (set *spray_pqueue_t, x i64) -> bool
begin
    var preds [20]*node_t;
    var succs [20]*node_t;
    var succ *node_t;
    while true do
        if false == find(set, x, preds, succs) then
            return false;
        else
            var node_to_remove = succs[0];
            var marked bool;
            for var level = node_to_remove.toplevel;
                level >= 1;
                --level
            do
                succ = node_to_remove.next[level];
                marked = is_marked(succ);
                while !marked do
                    __builtin_cas(&node_to_remove.next[level], unmark(succ),
                                  mark(succ));
                    succ = node_to_remove.next[level];
                    marked = is_marked(succ);
                od
            od
            succ = node_to_remove.next[0];
            marked = is_marked(succ);
            while true do
                var i_marked_it = __builtin_cas(&node_to_remove.next[0],
                                                unmark(succ), mark(succ));
                succ = succs[0].next[0];
                marked = is_marked(succ);
                if i_marked_it then
                    // FIXME: succs[0]?  Should retire node_to_remove?
                    retire succs[0];
                    find(set, x, preds, succs);
                    return true;
                elif marked then
                    return false;
                fi
            od
        fi
    od
end

/** Remove a node, lock-free, from the skiplist.  Leak the memory.
 */
export
def spray_pqueue_leaky_remove (set *spray_pqueue_t, x i64) -> bool
begin
    var preds [20]*node_t;
    var succs [20]*node_t;
    var succ *node_t;
    while true do
        if false == find(set, x, preds, succs) then
            return false;
        else
            var node_to_remove = succs[0];
            var marked bool;
            for var level = node_to_remove.toplevel;
                level >= 1;
                --level
            do
                succ = node_to_remove.next[level];
                marked = is_marked(succ);
                while !marked do
                    __builtin_cas(&node_to_remove.next[level], unmark(succ),
                                  mark(succ));
                    succ = node_to_remove.next[level];
                    marked = is_marked(succ);
                od
            od
            succ = node_to_remove.next[0];
            marked = is_marked(succ);
            while true do
                var i_marked_it = __builtin_cas(&node_to_remove.next[0],
                                                unmark(succ), mark(succ));
                succ = succs[0].next[0];
                marked = is_marked(succ);
                if i_marked_it then
                    find(set, x, preds, succs);
                    return true;
                elif marked then
                    return false;
                fi
            od
        fi
    od
end

/** Remove a node, lock-free, from the skiplist.
 */
export
def spray_pqueue_pop_min (seed *u64, set *spray_pqueue_t) -> bool
begin
  while true do
    var cleaner = (fast_rand(seed) % (set.config.thread_count + 1)) == 0;
    if cleaner then
        for var curr = unmark(set.head.next[0]); curr != &set.tail; curr = unmark(curr.next[0]) do
            if curr.state == DELETED then
                spray_pqueue_remove(set, curr.priority);
            fi
        od
    fi

    var empty bool = set.head.next[0] == &set.tail;
    if empty then
      return false;
    fi

    var node = spray(seed, set);
    
    // Spray failed, try again
    if node.state == PADDING || node.state == DELETED then
      continue;
    fi

    var res = __builtin_cas(&node.state, ACTIVE, DELETED);
    // We didn't delete a node from the list.
    if res then
      return true;
    fi
  od
end

/** Remove a node, lock-free, from the skiplist.  Leak the memory.
 */
export
def spray_pqueue_leaky_pop_min (seed *u64, set *spray_pqueue_t) -> bool
begin
  while true do
    var cleaner = (fast_rand(seed) % (set.config.thread_count + 1)) == 0;
    if cleaner then
        for var curr = unmark(set.head.next[0]); curr != &set.tail; curr = unmark(curr.next[0]) do
            if curr.state == DELETED then
                spray_pqueue_leaky_remove(set, curr.priority);
            fi
        od
    fi
      
    var empty bool = set.head.next[0] == &set.tail;
    if empty then
        return false;
    fi
    
    var node = spray(seed, set);
    
    // Spray failed, try again
    if node.state == PADDING || node.state == DELETED then
        continue;
    fi

    var res = __builtin_cas(&node.state, ACTIVE, DELETED);
    // We didn't delete a node from the list.
    if res then
        return true;
    fi
  od
end

def fast_rand (seed *u64) -> u64
begin
    var val = seed[0];
    if val == 0 then val = 1; fi

    val ^= val << 6;
    val ^= val >> 21;
    val ^= val << 7;

    seed[0] = val;
    return val;
end

def random_level (seed *u64, max u32) -> u32
begin
    var level = 1;
    while fast_rand(seed) % 2 == 0 && level < max do
        ++level;
    od
    return level - 1;
end

def spray (seed *u64, set *spray_pqueue_t) -> *node_t
begin
  var cur_node *node_t = set.padding_head;
  var D = set.config.descend_amount;
  for var H = set.config.start_height; H >= 0; H = H - D do
    var jump = (fast_rand(seed) % set.config.max_jump) + 1;
    while jump > 0 do
      var next = unmark(cur_node.next[H]);
      if next == nil then
        break;
      fi
      if next == &set.tail then
        break;
      fi
      cur_node = next;
      jump--;
    od
  od
  return cur_node;
end 

def find (set *spray_pqueue_t, priority i64, preds [20]*node_t, succs [20]*node_t) -> bool
begin
    var marked, snip bool;
    var pred, curr, succ *node_t = nil, nil, nil;
retry:
    while true do
        pred = &set.head;
        for var level = 19; level >= 0; --level do
            curr = unmark(pred.next[level]);
            while true do
                { succ, marked } = ref_and_markbit(curr.next[level]);
                while marked do
                    snip = __builtin_cas(&pred.next[level], curr, succ);
                    if !snip then goto retry; fi
                    curr = unmark(pred.next[level]);
                    { succ, marked } = ref_and_markbit(curr.next[level]);
                od
                if curr.priority < priority then
                    pred = curr;
                    curr = succ;  
                else
                    break;
                fi
            od
            preds[level] = pred;
            succs[level] = curr;
        od
        return curr.priority == priority;
    od
end

def ref_and_markbit (ptr *node_t) -> { *node_t, bool } =
    { unmark(ptr), is_marked(ptr) };

def mark (ptr *node_t) -> *node_t =
    cast(type *node_t, 0x1I64 | cast(type i64, ptr));

def unmark (ptr *node_t) -> *node_t =
    cast(type *node_t, 0xFFFFFFFFFFFFFFFEI64 & cast(type i64, ptr));

def is_marked (ptr *node_t) -> bool =
    cast(type bool, 0x1I64 & cast(type i64, ptr));
